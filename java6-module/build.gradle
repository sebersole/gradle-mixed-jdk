version = '1.0'

repositories {
    mavenCentral()
}

configurations {
    jaxb {
        description = 'Dependencies for running ant xjc (jaxb class generation)'
    }
}

dependencies {
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.5'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics:0.6.3'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.6.3'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-jaxb-xjc:2.2.4-1'

//    jaxb2_jaxb:     'org.jvnet.jaxb2_commons:jaxb2-basics-jaxb:2.2.4-1',
}

ext {
    jaxbTargetDir = file( "${buildDir}/generated-src/jaxb/main" )
}


task jaxb {
    ext {
        // input schemas
        cfgXsd = file( 'src/main/resources/org/hibernate/xsd/cfg/legacy-configuration-4.0.xsd')

        // input bindings
        cfgXjb = file( 'src/main/xjc-bindings/hbm-configuration-bindings.xjb' )
    }

    // configure Gradle up-to-date checking
    inputs.files( [cfgXsd, cfgXjb] )
    outputs.dir( jaxbTargetDir )

    // perform actions
    doLast {
        jaxbTargetDir.mkdirs()

        ant.taskdef(name: 'xjc', classname: 'org.jvnet.jaxb2_commons.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
        ant.jaxbTargetDir = jaxbTargetDir

        // hibernate-configuration
        ant.xjc(
                destdir: '${jaxbTargetDir}',
                binding: cfgXjb.path,
                schema: cfgXsd.path
        )
    }
}

sourceSets.main {
    java.srcDir jaxbTargetDir
}

compileJava.dependsOn jaxb